#!/usr/bin/perl
use strict;
use warnings;

sub usage
{
	print <<EOF;
cogd (a Bolo collector)
USAGE: cogd [options]

options:
   -h, --help               Show this help screen
   -p, --prefix PREFIX      Use the given metric prefix
                            (FQDN is used by default)
   -l, --log FILENAME       Log to parse cogd stats data from.
                            (Defaults to /var/log/daemon.log)

For this collector to work, cogd needs to be logging at
NOTICE level or higher.

EOF
	exit 0;
}

use POSIX;
use Getopt::Long qw/:config bundling/;
our %OPTIONS = ();
GetOptions(\%OPTIONS, qw/
	help|h|?
	debug|D

	prefix|p=s
	log|l=s
/) or usage();
usage() if $OPTIONS{help};
chomp($OPTIONS{prefix} = qx(hostname -f))
	unless $OPTIONS{prefix};
$OPTIONS{log} = "/var/log/daemon.log"
	unless $OPTIONS{log};

my $STATE = "$OPTIONS{prefix}:cogd";

open my $fh, "<", $OPTIONS{log}
	or exit(1);
my $line;
while (<$fh>) {
	next unless m/\bcogd\[\d+\]: STATS\(ms\): (.*)/;
	$line = $1;
}
close $fh;
if ($line =~ m/^
		 connect=(\d+),\s    hello=(\d+),\s    preinit=(\d+),\s
		copydown=(\d+),\s    facts=(\d+),\s  getpolicy=(\d+),\s
		   parse=(\d+),\s  enforce=(\d+),\s    cleanup=(\d+)/x) {

	my $now = time;
	printf "SAMPLE %i %s:connect %0.3f\n",   $now, $STATE, $1 / 1000;
	printf "SAMPLE %i %s:hello %0.3f\n",     $now, $STATE, $2 / 1000;
	printf "SAMPLE %i %s:preinit %0.3f\n",   $now, $STATE, $3 / 1000;
	printf "SAMPLE %i %s:copydown %0.3f\n",  $now, $STATE, $4 / 1000;
	printf "SAMPLE %i %s:facts %0.3f\n",     $now, $STATE, $5 / 1000;
	printf "SAMPLE %i %s:getpolicy %0.3f\n", $now, $STATE, $6 / 1000;
	printf "SAMPLE %i %s:parse %0.3f\n",     $now, $STATE, $7 / 1000;
	printf "SAMPLE %i %s:enforce %0.3f\n",   $now, $STATE, $8 / 1000;
	printf "SAMPLE %i %s:cleanup %0.3f\n",   $now, $STATE, $9 / 1000;
}
exit 0;
